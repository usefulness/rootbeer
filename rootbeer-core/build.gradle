plugins {
    alias(libs.plugins.starter.library.android)
    alias(libs.plugins.kotlinx.binarycompatibility)
    id("com.starter.publishing")
}

description = "Simple to use root checking Android library and sample app"

android {
    ndkVersion = "28.1.13356709"

    defaultConfig {
        namespace = "io.github.usefulness.rootbeer"
        minSdkVersion = 23

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }
    lint {
        warningsAsErrors = true
        checkReleaseBuilds = false
        disable("GradleDependency")
    }
    kotlinOptions {
        freeCompilerArgs += '-Xexplicit-api=strict'
    }
    compileOptions {
        sourceCompatibility = JavaVersion.toVersion(commonConfig.javaVersion.majorVersion.toInteger())
        targetCompatibility = JavaVersion.toVersion(commonConfig.javaVersion.majorVersion.toInteger())
    }
    publishing {
        singleVariant("release")
    }
    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }
}

androidComponents {
    beforeVariants(selector().withBuildType("release")) { builder ->
        builder.enable = true
    }
}

tasks.withType(JavaCompile).configureEach {
    options.headerOutputDirectory.set(layout.projectDirectory.file("src/main/jni").asFile)
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

dependencies {
    compileOnly(libs.androidx.annotation)
}
